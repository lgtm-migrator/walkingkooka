(* general expressions *)
EXPRESSION 		            = TEXT | FUNCTION | RANGE | CELL | BINARY_EXPRESSION | GROUP | NEGATIVE | PERCENTAGE | NUMBER;

BINARY_EXPRESSION           = BINARY_SUB_EXPRESSION, {[ WHITESPACE ], BINARY_OPERATOR, [ WHITESPACE ], BINARY_SUB_EXPRESSION};
BINARY_OPERATOR             = EQUALS_SYMBOL | NOT_EQUALS_SYMBOL | GREATER_THAN_EQUALS_SYMBOL | GREATER_THAN_SYMBOL | LESS_THAN_EQUALS_SYMBOL | LESS_THAN_SYMBOL | PLUS_SYMBOL | MINUS_SYMBOL | MULTIPLY_SYMBOL | DIVIDE_SYMBOL | POWER_SYMBOL | BETWEEN_SYMBOL;
BINARY_SUB_EXPRESSION       = FUNCTION | CELL | GROUP | NEGATIVE | PERCENTAGE | NUMBER;

(* cell column/row OR label *)
CELL			            = LABEL_NAME | COLUMN_ROW;

RANGE                       = CELL, [ WHITESPACE ], BETWEEN_SYMBOL, [ WHITESPACE ], CELL;

(* function aka function( 123, 456 ) *)
FUNCTION                    = FUNCTION_NAME, OPEN_PARENTHESIS_SYMBOL, [ WHITESPACE ], [FUNCTION_PARAMETERS], [ WHITESPACE ], CLOSE_PARENTHESIS_SYMBOL;
FUNCTION_PARAMETERS         = EXPRESSION, [{FUNCTION_PARAMETER_OTHERS}];
FUNCTION_PARAMETER_OTHERS   = [ WHITESPACE ], FUNCTION_PARAMETER_SEPARATOR_SYMBOL, [ WHITESPACE ], EXPRESSION;

GROUP 			            = OPEN_PARENTHESIS_SYMBOL, [ WHITESPACE ], EXPRESSION, [ WHITESPACE ], CLOSE_PARENTHESIS_SYMBOL;

(* NEGATIVE_EXPRESSION ... NUMBER must not have BINARY_EXPRESSION to avoid incorrect tokens. *)
NEGATIVE_EXPRESSION         = FUNCTION | CELL | GROUP | PERCENTAGE | NUMBER;
NEGATIVE		            = MINUS_SYMBOL, [ WHITESPACE ], NEGATIVE_EXPRESSION;

PERCENTAGE		            = NUMBER, PERCENT_SYMBOL;